{
pub(all) enum SubstItemDesc {
  StartPosOf(String)
  EndPosOf(String)
  StartPos
  EndPos
}

pub(all) struct SubstItem {
  start : Int
  end : Int
  desc : SubstItemDesc
}
}

rule scan_codeblock_rbrace[T : @lexbuf.IStringLexbuf](subst : Array[SubstItem], lexbuf : T) -> Unit {
  parse {
    "{" => {
      scan_codeblock_rbrace(subst, lexbuf)
      scan_codeblock_rbrace(subst, lexbuf)
    }
    "}" => { () }
    "$" (("startpos" | "endpos") as t1) "(" [' ' '\t']* ((['A'-'Z' 'a'-'z' '_'] ['A'-'Z' 'a'-'z' '0'-'9' '_']*) as t2) [' ' '\t']* ")" => {
      subst.push({ 
        start: $startpos, 
        end: $endpos, 
        desc: match t1 {
          "startpos" => StartPosOf(t2)
          "endpos" => EndPosOf(t2)
          _ => panic()
        }
      })
      scan_codeblock_rbrace(subst, lexbuf)
    }
    "$" (['A'-'Z' 'a'-'z' '_'] ['A'-'Z' 'a'-'z' '0'-'9' '_']* as t1) => {
      if t1 is ("startpos" | "endpos") {
        subst.push({ 
          start: $startpos, 
          end: $endpos, 
          desc: match t1 {
            "startpos" => StartPos
            "endpos" => EndPos
            _ => panic()
          }
        })
      }
      scan_codeblock_rbrace(subst, lexbuf)
    }
    _ => { scan_codeblock_rbrace(subst, lexbuf) }
    "" => { () }
  }
}

{
pub fn parse_codeblock(str : String) -> Array[SubstItem] {
  let lexbuf = @lexbuf.StringLexbuf::from_string(str)
  let subst = []
  scan_codeblock_rbrace(subst, lexbuf)
  subst
}
}
